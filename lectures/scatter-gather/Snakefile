import os

samples = ["sample1", "sample2"]

splits = 5
scatteritems = [f"{split:03d}" for split in list(range(1, splits+1))]

wildcard_constraints:
    scatteritems = "\\d+",
    sample = "\\w+"

rule all:
    input:
        expand("{sample}.rc.fastq", sample = samples)

rule scatter:
    output:
        expand("splits/{{sample}}/{{sample}}.{scatteritem}.fastq", scatteritem = scatteritems)
    input:
        "data/{sample}.fastq"
    log:
        "logs/{sample}.scatter.log"
    conda:
        "envs/seqkit.yml"
    params:
        splits = splits,
        outdir = lambda wildcards, output: os.path.dirname(output[0])
    shell:
        """ 
        seqkit split --by-part-prefix {wildcards.sample}. -p {params.splits} -O {params.outdir} {input} > {log} 2>&1
        """

rule rc:
    output:
        "rc/{sample}/{sample}.{scatteritem}.rc.fastq"
    input:
        "splits/{sample}/{sample}.{scatteritem}.fastq"
    log:
        "logs/{sample}.{scatteritem}.rc.log"
    conda:
        "envs/seqkit.yml"
    shell:
        """
        seqkit seq --seq-type DNA --reverse --complement {input} > {output} 2> {log}
        """

rule gather:
    output:
        "{sample}.rc.fastq"
    input:
        expand("rc/{{sample}}/{{sample}}.{scatteritem}.rc.fastq", scatteritem = scatteritems)
    shell:
        """
        cat {input} > {output}
        """
